import org.gradle.api.publish.maven.MavenPublication

archivesBaseName = "${rootProject.antimatter_archive_base_name}-fabric"
version = "${rootProject.minecraft_version}-${project.mod_version}"

def generatedResources = file("src/generated/resources")
def accessWidenerFile = project(":antimatter-common").file("src/main/resources/antimatter.accesswidener")

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

repositories {
    maven {
        url = uri("https://storage.googleapis.com/devan-maven/")
    }
}

loom {
    accessWidenerPath = accessWidenerFile
}

sourceSets {
    main {
        resources {
            srcDir generatedResources
        }
    }
}

dependencies{
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
    modCompileOnly("dev.latvian.mods:kubejs-fabric:${rootProject.kubejs_version}")
    common(project(path: ":tesseract-common", configuration: "namedElements")) { transitive false }
    implementation(project(path: ":tesseract-fabric", configuration: "namedElements")) { transitive false }
    modImplementation(include("io.github.feltmc:fabricated-forge-caps:${rootProject.fabricated_forge_hash}"))
    modImplementation(include("io.github.feltmc:fabricated-forge-modeldata:${rootProject.fabricated_forge_hash}"))
    modImplementation(include("io.github.feltmc:fabricated-forge-tag-extensions:${rootProject.fabricated_forge_hash}"))
    modImplementation(include("com.github.FeltMC.Felt-API:felt-recipe-api:2a689af1cb"))
    modImplementation(include("com.github.Trinsdar.Felt-API:felt-item-api:9695183f48")) { transitive false }
    common(project(path: ":antimatter-common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":antimatter-common", configuration: "transformProductionFabric")) { transitive false }
    modImplementation(include("net.devtech:arrp:0.5.7"))
    modImplementation(include("com.github.Trins-mods:CT-Fabric-Fluid-Support:${project.ct_fabric_fluid_support_hash}")){
        transitive = false
    }
    modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:${rootProject.rei_version}"
    modCompileOnly("mezz.jei:jei-${rootProject.minecraft_version}-common-api:${rootProject.jei_version}")
    modCompileOnly("mezz.jei:jei-${rootProject.minecraft_version}-fabric-api:${rootProject.jei_version}")
    modRuntimeOnly("teamreborn:energy:${rootProject.energy_version}") {
        transitive = false
    }
    implementation("com.electronwill.night-config:toml:${rootProject.nightconfig_version}")
    implementation("com.electronwill.night-config:core:${rootProject.nightconfig_version}")
    modRuntimeOnly("com.terraformersmc:modmenu:${rootProject.mod_menu_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modImplementation("net.minecraftforge:forgeconfigapiport-fabric:3.2.4")
    modCompileOnly("curse.maven:the-one-probe-fabric-581847:${project.top_fabric_file}")
}

architectury {
    fabric()
}

task copyAccessWidener(type: Copy) {
    from accessWidenerFile
    into generatedResources
}

processResources {
    dependsOn copyAccessWidener

    def toReplace = [
            "version"             : project.version
    ]

    inputs.properties toReplace
    filesMatching("fabric.mod.json") {
        expand toReplace
    }
}
sourcesJar {
    from(project(':antimatter-common').sourceSets.main.allSource) {
        exclude("antimatter.accesswidener") // duplicate on fabric, unneeded on forge
    }
}

publishing {
    publications {
        mavenJava(org.gradle.api.publish.maven.MavenPublication) {
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }

            afterEvaluate {
                artifact remapJar
            }
        }
    }

    repositories {
        def isCI = System.getenv("GITHUB_ACTION")
        def isRELEASE = System.getenv("GITHUB_RELEASE")
        if (isCI && isRELEASE) {
            maven {
                name = "GTMaven"
                url = "https://repo.repsy.io/mvn/trinsdar/gregtech-intergalactical/"
                credentials {
                    username = System.getenv("MAVEN_USERNAME")
                    password = System.getenv("MAVEN_PASSWORD")
                }
            }
        } else {
            maven { url "file:///${project.projectDir}/mcmodsrepo"}
        }
    }
}
