buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            name = 'parchment'
            url = 'https://maven.parchmentmc.org' 
        }
        maven {
            name = 'forge'
            url = 'https://files.minecraftforge.net/maven'
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
        classpath 'org.parchmentmc:librarian:1.+'
    }
}

repositories {
    //JEI
    maven { url 'https://dvs1.progwml6.com/files/maven' }
    /*maven {
        url = "https://maven.speiger.com/repository/main"
    }*/
    //GT.
    maven { url 'https://jitpack.io' }

    //Needed for intellij
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}
def isCI = System.getenv("GITHUB_ACTION")
def isRELEASE = System.getenv("GITHUB_RELEASE")
def gitHash() {
    String hash = System.getenv("GITHUB_SHA")
    if (hash != null) return hash.substring(0,8)
    return ""
}

def isAntimatterCI() {
    String val = System.getenv("GITHUB_WORKFLOW")
    return val != null && val.toLowerCase().startsWith("antimatter")
}

apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'org.parchmentmc.librarian.forgegradle'

version = "${minecraft_version}-${mod_version}"
if (isCI) {
    version = version + "-" + gitHash()
}
group = 'com.github.gregtech-intergalactical' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "${modid}"
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

//Print out JVM information so that we know what version is running. Extreamly useful for people to know when helping you.
println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))


boolean hasTesseract = findProject(':TesseractAPI') != null

if (hasTesseract) {
    println('TesseractAPI in local workspace')
}
boolean isWorkspace = hasTesseract
String getenv() {
    boolean vscode = gradle.startParameter.taskNames[0].equals("genVSCodeRuns")  || gradle.startParameter.taskNames[0].equals(":genVSCodeRuns")
    boolean hasTesseract = findProject(':TesseractAPI') != null

    String resourcePath = vscode ? 'bin/main' : 'build/resources/main'
    String codePath = vscode ? 'bin/main' : 'build/classes/java/main'
    String s = "antimatter%%${project.file(resourcePath)}" + getPathSeparator() + "antimatter%%${project.file(codePath)}"
    if (hasTesseract) {
        s += getPathSeparator() + "tesseract%%${project(':TesseractAPI').file(resourcePath)}" + getPathSeparator() + "tesseract%%${project(':TesseractAPI').file(codePath)}"
    }
    return s
}
import org.apache.tools.ant.taskdefs.condition.Os

def getPathSeparator() {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        return ";"
    }
    return ":"
}

minecraft {
    // The mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   Snapshot are built nightly.
    // stable_#            Stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: "parchment", version: "${mappings_version}"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    //accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')
            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'DEBUG'
            property 'fml.earlyprogresswindow', 'false'

            arg "-mixin.config=antimatter.mixins.json"
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            if (isWorkspace) {
                environment('MOD_CLASSES', this.getenv())
            }
            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
            mods {
                antimatter {
                    source sourceSets.main
                }
                if (hasTesseract) {
                    tesseract {
                        source project(':TesseractAPI').sourceSets.main
                    }
                }
            }
        }

        server {
            workingDirectory project.file('run')
            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'DEBUG'
            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
            property 'fml.earlyprogresswindow', 'false'

            // Disable GUI by default, the IDE console can be used to enter commands
            arg '-nogui'
            if (isWorkspace) {
                environment('MOD_CLASSES', this.getenv())
            }
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            arg "-mixin.config=antimatter.mixins.json"
            mods {
                antimatter {
                    source sourceSets.main
                }
                if (hasTesseract) {
                    tesseract {
                        source project(':TesseractAPI').sourceSets.main
                    }
                }
            }
        }
/*
        data {
            workingDirectory project.file('run')

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'DEBUG'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
            property 'fml.earlyprogresswindow', 'false'

            arg "-mixin.config=antimatter.mixins.json"
            args '--mod', 'antimatter', '--all', '--output', project.file('src/generated/resources/'), '--existing', sourceSets.main.resources.srcDirs[0]

            mods {
                antimatter {
                    source sourceSets.main
                }
                if (hasTesseract) {
                    tesseract {
                        source project(':TesseractAPI').sourceSets.main
                    }
                }
            }
        }*/
    }
}


dependencies {
    // Specify the version of Minecraft to use, If this is any group other then 'net.minecraft' it is assumed
    // that the dep is a ForgeGradle 'patcher' dependency. And       it's patches will be applied.
    // The userdev artifact is a special name and will get all sorts of transformations applied to it.
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    //GT
    if (hasTesseract) {
       implementation project(path: ':TesseractAPI')
    } else {
        implementation fg.deobf("com.github.GregTech-Intergalactical:TesseractAPI:${tesseract_version}")
    }

    //JEI
    compileOnly fg.deobf("mezz.jei:jei-${jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-${jei_version}")

    //MIXIN
    if (System.getProperty('idea.sync.active') != 'true') {
        annotationProcessor 'org.spongepowered:mixin:0.8.5-SNAPSHOT:processor'
    }


    /** KUBEJS STUFF **/
   // implementation fg.deobf("curse.maven:rhino-416294:${rhino_file}")
   // implementation fg.deobf("curse.maven:architecturyforge-419699:${architectury_file}")
  //  implementation fg.deobf("curse.maven:kubejs-238086:${kubejs_file}")
//    implementation fg.deobf("curse.maven:top-245211:${top_file}")
}

// Example configuration to allow publishing using the maven-publish task
// This is the preferred method to reobfuscate your jar file
//jar.finalizedBy('reobfJar')
// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing

mixin {
    add sourceSets.main, 'antimatter.refmap.json'
}
/*
task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveBaseName.set(project.archivesBaseName)
    archiveVersion.set("${project.version}")
    archiveClassifier.set('sources')
}*/
jar {
    zip64 true
    manifest {
        attributes([
                "Specification-Title": project.name,
                "Specification-Vendor": "GregTech Intergalactical",
                "Specification-Version": project.version,
                "Implementation-Title": project.name,
                "Implementation-Version": project.version,
                "Implementation-Vendor": "GregTech Intergalactical",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'MixinConfigs': 'antimatter.mixins.json'
            ])
    }
}
if (isAntimatterCI()) {
    jar.finalizedBy('reobfJar')
}


artifacts {
    archives jar//, sourcesJar
}

if (isAntimatterCI()) {
    println("In CI mode")
}
publishing {
    tasks.publish.dependsOn 'build', 'reobfJar'
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }

    repositories {
        if (isCI && isRELEASE) {
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/GregTech-Intergalactical/AntimatterAPI"
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        } else {
            maven { url "file:///${project.projectDir}/mcmodsrepo"}
        }
    }
}


//jar.finalizedBy('reobfJar')
/*configurations.compile {
    exclude group: 'it.unimi.dsi', module: 'fastutil'
}*/

/*shadowJar {
    configurations = [project.configurations.shadowCommon]
    classifier ''
}*/

//jar.classifier = 'nodep'
