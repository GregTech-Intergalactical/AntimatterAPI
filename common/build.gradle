plugins {
    id "xyz.wagyourtail.unimined" version "1.3.9-SNAPSHOT"
}
archivesBaseName = "${rootProject.antimatter_archive_base_name}-common"

configurations {
    modCompileOnly
    compileOnly.extendsFrom modCompileOnly
    modRuntimeOnly
    runtimeOnly.extendsFrom modRuntimeOnly
}

unimined.minecraft {
    version minecraft_version
    accessWidener {
        accessWidener(file("src/main/resources/antimatter.accesswidener"))
    }

    defaultRemapJar = false

    mappings {
        intermediary()
        mojmap()
        devFallbackNamespace "official"
        devNamespace "mojmap"
        parchment(rootProject.minecraft_version, rootProject.mappings_version)
    }

    mods {
        remap(configurations.modCompileOnly){
            catchAWNamespaceAssertion()
            namespace("intermediary")
        }
        remap(configurations.modRuntimeOnly){
            catchAWNamespaceAssertion()
            namespace("intermediary")
        }
        modImplementation {
            catchAWNamespaceAssertion()
            namespace("intermediary")
        }
    }
}

repositories {
    maven {
        url = uri("https://storage.googleapis.com/devan-maven/")
    }
    maven { url "https://maven.bai.lol" }
}

dependencies{
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    compileOnly("com.blamejared.crafttweaker:CraftTweaker-common-1.18.2:${rootProject.crafttweaker_version}")
    modCompileOnly("dev.latvian.mods:kubejs-fabric:${rootProject.kubejs_version}")
    implementation(project(path: ":tesseract-common"))
    modImplementation("io.github.fabricators_of_create:Porting-Lib:${rootProject.port_lib_version}-beta+1.18.2-stable"){
        exclude(group: "net.fabricmc.fabric-api")
        exclude(group: "com.github.LlamaLad7")
    }
    modImplementation("net.feltmc:felt-player-item-api:1.1.0"){
        transitive = false
    }
    modImplementation("net.feltmc:felt-enchanting-api:1.1.0") { transitive false }
    implementation(annotationProcessor("io.github.llamalad7:mixinextras-common:0.2.0-beta.11"))
    modImplementation("io.github.trinsdar:CT-Fabric-Fluid-Support:${project.ct_fabric_fluid_support_hash}")
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api:${rootProject.rei_version}"
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-default-plugin:${rootProject.rei_version}"
    modCompileOnly("mezz.jei:jei-${rootProject.minecraft_version}-common-api:${rootProject.jei_version}")
    modCompileOnly("io.github.trinsdar:arrp:0.6.7")
    implementation("com.gtnewhorizon.structurelib:structurelib-common:${project.structurelib_version}-${project.minecraft_version}")
    modImplementation "maven.modrinth:carbon-config:${project.carbon_config_fabric_version}"
    modCompileOnly("curse.maven:the-one-probe-fabric-581847:${project.top_fabric_file}")
    modImplementation("earth.terrarium:botarium-common-1.18.2:${project.botarium_version}")
    modImplementation "com.teamresourceful.resourcefullib:resourcefullib-common-1.18.2:${project.resourcefullib_version}"
    modCompileOnly("com.simibubi.create:create-fabric-${minecraft_version}:0.5.0.i-944+1.18.2"){
        transitive = false
    }

}

publishing {
    publications {
        mavenJava(org.gradle.api.publish.maven.MavenPublication) {
            from components.java
        }
    }

    repositories {
        def isCI = System.getenv("GITHUB_ACTION")
        def isRELEASE = System.getenv("GITHUB_RELEASE")
        if (isCI && isRELEASE) {
            maven {
                name = "GTMaven"
                url = "https://repo.repsy.io/mvn/trinsdar/gregtech-intergalactical/"
                credentials {
                    username = System.getenv("MAVEN_USERNAME")
                    password = System.getenv("MAVEN_PASSWORD")
                }
            }
        } else {
            maven { url "file:///${project.projectDir}/mcmodsrepo"}
        }
    }
}
